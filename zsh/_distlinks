#compdef distlinks
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'(1 -)'{'-h','-H'}'[help]' \
'(-f)-1[flatten structure: symlinks for .obsolete/.save*]' \
'(-1)-f[force flatten: symlinks for .save*, move from .obsolete]' \
'(-R)-r[(is default) remove matching/strange symlinks if possible]' \
'(-r)-R[do not remove matching/strange symlinks]' \
'(-C)-c[(is default) cleanup dead links and empty .save* dirs]' \
'(-c)-C[no cleanup of dead links or empty .save* dirs]' \
'-k[keep existing symlinks]' \
'-a[create all links, even if they exist in distfiles or .obsolete)]' \
'-T[omit executing the trickyfetch script]' \
'-v[verbose]' \
'-O[do not make portage the owner of the files]' \
'1::command:->cmds'
local ret=$?
case $state in
cmds)
	local cmds
	cmds=(
	'create:create symlinks in $DISTDIR'
	'remove:remove symlinks in $DISTDIR'
	'help:print help'
	)
	_describe -t commands 'command' cmds && ret=0;;
esac
return ret
